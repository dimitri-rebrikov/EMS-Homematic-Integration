string funcName = "ThermostatList";
var func = dom.GetObject(funcName);

string sysvName = "SysVar.BoilerHeatingDisabled";
var sysv = dom.GetObject(sysvName);
WriteLine("Value for: " # sysvName # "=" # sysv.Value());

real permanentOnTemp = 30.5;
real winterModeTemp = 20.0;

boolean heatingDisabled = sysv.Value();

string chId; 
foreach(chId, func.EnumUsedIDs()) {
  var ch = dom.GetObject(chId);
  !WriteLine(ch.Name() # ": " # ch.ID() # ": " # ch.Device());
  var dvc = dom.GetObject(ch.Device());
  !WriteLine("=>" # dvc.Name() # ": " # dvc.ID() # ": '" # dvc.HssType() # "'");
  string dpId;
  var dpTemp;
  var dpModeAuto;
  var dpModeManu;
  var dpModeAutoValue;
  var dpModeManuValue;  
  foreach(dpId, ch.DPs()) {
    var dp = dom.GetObject(dpId);
    !WriteLine("->'" # dp.Name() # "' : " # dp.Value());
	if(dvc.HssType() == "HM-CC-RT-DN") {
		if(dp.Name().EndsWith(".SET_TEMPERATURE")) {
			dpTemp = dp;
		} elseif (dp.Name().EndsWith(".AUTO_MODE")) {
			dpModeAuto = dp;
			dpModeAutoValue = 1;
		} elseif (dp.Name().EndsWith(".MANU_MODE")) {
			dpModeManu = dp;
			dpModeManuValue = 1;
		}
	} elseif(dvc.HssType() == "HmIP-eTRV-2") {
		if(dp.Name().EndsWith(".SET_POINT_TEMPERATURE")) {
			dpTemp = dp;
		} elseif (dp.Name().EndsWith(".CONTROL_MODE")) {
			dpModeAuto = dp;
			dpModeManu = dp;
			dpModeAutoValue = 0;
			dpModeManuValue = 1;
		}
	}
  }
  if(heatingDisabled && (dpTemp.Value() < permanentOnTemp)){
	WriteLine(dvc.Name() # " activate summer mode");
	if(dpModeManu.Value() != dpModeManuValue) {
		WriteLine("Set " # dpModeManu.Name() # " to " # dpModeManuValue);
		dpModeManu.State(dpModeManuValue);
	}
	WriteLine("Set " # dpTemp.Name() # " to " # permanentOnTemp);
	dpTemp.State(permanentOnTemp);
	WriteLine(dvc.Name() # " summer mode activated");
  } elseif((!heatingDisabled)  && (dpTemp.Value() >= permanentOnTemp)) {
    WriteLine(dvc.Name() # " activate winter mode");
	if(dpModeAuto.Value() != dpModeAutoValue) {
		WriteLine("Set " # dpModeAuto.Name() # " to " # dpModeAutoValue);
		dpModeAuto.State(dpModeAutoValue);
	}
	WriteLine("Set " # dpTemp.Name() # " to " # winterModeTemp);
	dpTemp.State(winterModeTemp);
    WriteLine(dvc.Name() # " winter mode activated");
  } else {
	WriteLine(dvc.Name() # " change nothing. Temp: " + dpTemp.Value() );
  }
}
